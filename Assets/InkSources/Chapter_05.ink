INCLUDE GameData.ink

-> start

=== start ===
# Location room_main

После встречи с Бамблби, ты в смятении.
Чтобы успокоиться, открываешь групповой чат класса.

Лидер: О, вижу новые лица в нашем чате! #Р
ЛидерПара: Ты всегда убегаешь от одноклассников? 

- Я: (Я могу...)
*   [(...отшутиться.) @good]
    Я: О, так это были вы? #У
    Я: Я просто {isBoy(): бегал, разминался| бегала, разминалась}. 
    Я: Даже не {isBoy(): заметил| заметила}, что меня кто-то звал. 
    Лидер: Ха, мне нравится твоё чувство юмора! #Р
    ЛидерПара: Просто знай, кто тут главный и всё будет в ажуре. 
    ~ ChangeRelPoints(Leader_RelPoints, 2)
    -> dialog1

*   [(...сказать, что это было нечестно.) @bad]
    Я: Это было нечестно! Вас было больше! #З
    Лидер: Как хорошо, что ты {isBoy(): заметил| заметила}.
    Лидер: Нас всегда больше. 
    ЛидерПара: По этому не высовывайся! #З
    -> dialog1

==dialog1==
Лидер: Суди {isBoy(): сам| сама},  ну, кто ты есть? 
Денис: Вот у меня батя владеет пятью магазинами. 
Денис: По сравнению с моим домом, твой — конура. 
Настя: А у меня 73К подписчиков в инсте.
Настя: Ты, походу, таких чисел даже не {isBoy(): видел| видела}.
Приспешник: Тут крутые тусуются! Ясно тебе?! #З
Лидер: Ну, чем похвастаешься?
ЛидерПара: Или ты {isBoy(): такой | такая} же лошара, как Свин?
Я: Я спортом занимаюсь.
Приспешник: Шахматами? ))))) #Р
Лидер: Не влезай! Пусть скажет. #З
Я: Я {isBoy(): играл за футбольную команду школы.| занималась акробатическими танцами.}
ЛидерПара: На скамейке запасных! )))) #Р
Я: {isBoy(): Забил больше всех мячей| Заняла первое место} в прошлом году.
Лидер: Врёшь!
Я: Не вру! #З
Приспешник: Врунишка! Врунишка! #З
Лидер: Докажи. Скоро городские соревнования. 
Лидер: Будешь в них участвовать — поверим. 
Я: А вот и докажу! #З

Ты закрываешь чат и идёшь на кухню. 

# Location kitchen

Отец, как раз пьёт свой вечерний кофе. 

- Я: (Я могу...)
*   [(...сразу спросить про соревнования.)]
    Я: Пап, я хочу участвовать в местных соревнованиях.
    Отец: Похвально! Тебя уже в команду взяли? #Р
    Я: Пока нет. Я ещё даже команду не {isBoy(): нашёл| нашла}.
    Отец: А что мешает? #У
    -> sport

*   [(...начать издалека.)]
    Я: Пап, ты помнишь, что я в секции {isBoy(): занимался| занималась}?
    Отец: Конечно, помню. Я всегда гордился твоими успехами. #Р
    Я: Может быть я здесь тоже буду заниматься...
    Отец: Я только за! Уже {isBoy(): нашёл| нашла} спортивный клуб?
    Я: Пока нет.
    Отец: А что мешает? #У
    -> sport

==sport==
Я: У меня формы нет. Давай завтра купим, а?
Отец: Чтобы она в шкафу лежала? 
Отец: Нет уж, {isBoy(): сын| дочь}. 
Отец: Ты сперва в команду попади, а потом и форма будет.
- Я: (Я могу...)
*   [(...согласиться с отцом.) @good]
    Я: Ну, ладно.
    Отец: Вот и хорошо. Не затягивай с поиском команды!
    ~ ChangeRelPoints(Parents_RelPoints, 2)
    -> dad

*   [(...начать спорить.) @bad]
    Я: Пап, тебе что жалко? 
    Отец: Нет, но и разбрасываться деньгами я не буду. 
    Я: Пап, ты же поддерживаешь мои увлечения спортом. 
    Я: Вот не купишь форму, не буду заниматься! #З
    Отец: Вот только без шантажа! #З
    Отец: Это моё последнее слово. 
    ~ ChangeRelPoints(Parents_RelPoints, -2)
    -> dad

==dad==
Отец: Кстати, ты уже {isBoy(): видел| видела} наш большой экран? 
Я: Какой ещё экран? 
Отец: Я купил домашний кинотеатр! Последняя модель! #Р
Отец: Картинка — просто отвал башки! #Р
Я: Спасибо, пока не хочу смотреть телевизор.

# Location room_main

Ты снова листаешь ленту ВК. 
Денис: Видали мой новый байк? Самый крутой в городе! #Р
Приспешник: Ого! Завидки ))) #Р
Настя: У меня камера в телефоне 4К снимает. 
Настя: А ещё замедленнную съемку! Смотрите, как круто! #Р
Приспешник: Настюха, ты самый крутой видеоблогер! #Р
Тебе тоже хочется получить немного популярности. 

- Я: (Я могу...)
*   [(...выложить фотку на фоне домашнего кинотеатра.) @bad]
    Ты быстро делаешь селфи с большим экраном. 
    Я: Теперь подпись...
    Я: Так: «Мой скромный ТВ».
    Денис: Воу... Мощно... #У
    Приспешник: Офигеть! Вот это размер! #У
    Настя: Да, неплохо. На таком мои видео будут вообще крутыми. #Р
    ~ ChangeRelPoints(Leader_RelPoints, 2)
    Ты смотришь, как под фотографией увеличивается число лайков. 
    И лицо невольно расплывается в улыбке. 
    В этот момент приходит личное сообщение. 
    Дин: Очень глупо, {isBoy(): дружок| подруга}.  
    Я: Дин? Как ты нашёл мой аккаунт?
    Дин: Ха. Твой телефон был в моих руках. 
    Дин: Теперь я знаю о тебе всё! #Р
    
        - - Я: (Я могу...)
        * * [(...спросить, что не так.)]
        Я: И что я {isBoy(): сделал| сделала} глупого? #У
        Дин: Ты {isBoy(): выложил| выложила} фотку с дорогим кинотеатром.
        Я: И что?
        -> question
        
        * * [(...спросить, понравилась ли ему фотография.)]
        Я: Да, ты же хакер. 
        Я: Кстати, тебе понравилась моя фотка?
        Дин: Категорически нет. 
        Я: И что в ней плохого?
        -> question

*   [(...не выкладывать фотку на фоне домашнего кинотеатра.) @good]
    В последний момент ты передумываешь. 
    Домашний кинотеатр не сделает тебя популярным.
    Через пару минут приходит личное сообщение от {isBoy(): Вики| Вити}.
    Симпатия: Привет. 
    Я: Привет! #Р
    Симпатия: Я {isBoy(): видела, как ты вел| видел, как ты вела} себя сегодня после уроков.
    Я: Ну... Так бы повел себя каждый. 
    Симпатия: Вовсе нет. Не в нашем классе точно. 
    Я: Спасибо. 
    Симпатия: Я тут {isBoy(): полистала| полистал} твою страницу.
    Я: Скучно, наверное. Все хвастаются покупками, а я нет.
    Симпатия: И правильно. Селфи с покупками — это глупость. 
    ~ ChangeRelPoints(Love_RelPoints, 2)
    Я: Ты первый человек, кто так считает. 
    Симпатия: У меня брат двоюродный тоже так считает. 
    Симпатия: После того, как его ограбили. 
    Я: А это тут при чём? 
    Симпатия: А при том. Он целый месяц хвастался новым телефоном.
    Симпатия: Фотографировался с ним постоянно. 
    Симпатия: Его потом подкараулили на улице и телефон отобрали.
    Я: Может совпадение? 
    Симпатия: Так нет же.
    Симпатия: Поблагодарили даже за то, что сам наводку дал. 
    Симпатия: Теми фотографиями. 
    Я: А я и не думал, что так бывает. #П
    {isBoy(): Вика| Витя} замолкает. Тебе хочется продолжить разговор.
    -> boasting

==question==
Дин: То, что она в свободном доступе. Её видят все. 
Я: В этом и смысл, если ты не знал. 
Я: Выложить фотку, чтобы её видели все и ставили мне лайки. 
Дин: А ещё, чтобы знали, какие у тебя дорогие покупки. 
Дин: Потом посмотрели в твоём профиле адрес.
Дин: И узнали, где можно украсть классный домашний кинотеатр. 
Я: Да ладно... Ты просто меня пугаешь.
Дин: Проснись, {isBoy(): наивный чукотский парень| наивная чукотская девушка}.
Дин: Так совершается добрая половина квартирных краж!
Я: И что теперь делать? #П
Дин: Удали пост, пока его увидело мало людей.
Дин: И запомни, показывать нужно свои достижения, а не вещи.
Я: Ладно... #П
Вздыхая, ты удаляешь пост. 
Через пару минут приходит личное сообщение от {isBoy(): Вики| Вити}.
Симпатия: Привет. 
Я: Привет! #Р
Симпатия: Я {isBoy(): видела, как ты вел| видел, как ты вела} себя сегодня после уроков.
Я: Ну... Так бы повел себя каждый. 
Симпатия: Вовсе нет. Не в нашем классе точно. 
Я: Спасибо.
-> boasting

==boasting==
- Я: (Я могу...)
*   [(...похвалить её за доклад.) @good]
    Я: Мне понравился твой доклад. Интересный.
    Симпатия: Да так. Можно было сделать лучше.
    ~ ChangeRelPoints(Love_RelPoints, 2)
    -> talk

*   [(...спросить, что не так с классом.) @bad]
    Я: А что не так с вашим классом? #У
    Симпатия: Уже с твоим классом. 
    Я: Ой, да. Нужно ещё привыкнуть. 
    Симпатия: Да, нужно. И к постоянному давлению и травле тоже.
    Симпатия: Скоро поймёшь. #П
    -> talk
    
==talk==
Я: А чем ты увлекаешься, кроме учебы? 
Симпатия: Особо ни чем. 
Я: Так не бывает. Нужно же чем-то занимать свободное время. #У
Симпатия: Если оно есть. 
Я: В смысле?
Симпатия: Когда в семье нет отца, всё не так. #П
Симпатия: Приходится много делать по дому.
Симпатия: Если ты, конечно, знаешь, что это такое.
Я: Знаю. 
Симпатия: Ладно. Мне пора. Ещё поболтаем.
Я: Да, поболтаем. Пока!
Симпатия: Пока.
Ты {isBoy(): рад| рада} началу общения с {isBoy(): Викой| Витей}.
От приятных мыслей ты долго не можешь заснуть.
Затем приходит сообщение.
Бамблби: Завтра будь осторожен. 
->END