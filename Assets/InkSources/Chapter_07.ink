INCLUDE GameData.ink

-> start

=== start ===
# Location kitchen

Ты отдаёшь переходник отцу и идёшь на кухню.
Мама: Магазин, вроде, рядом. Долго {isBoy(): выбирал| выбирала}? # У
Я: Вроде того... 
Мама: Ладно тебе. Я всё понимаю. 
Мама: Наверняка {isBoy(): встретил| встретила} одноклассников. 
Я: Как ты угадала? # У
Мама: Я тоже когда-то училась в школе. 
Мама: Уже {isBoy(): подружился| подружилась} с кем-нибудь?
Я: Почти.
Мама: Как я рада! # Р
Мама: У меня никогда не получалось быстро заводить друзей.
Я: Кстати, я сегодня вечером с ними иду гулять. 
Мама: Ох, как неудачно... # П
Я: Что-то случилось?
Мама: Да, мы с папой уходим.
Мама: Так что тебе нужно будет посидеть дома с сестрой.
- Я: (Я могу...)
*    [(...возмутиться.) @bad]
     Я: А как же мои новые друзья?
     Мама: Погуляешь с ними в другой раз.
     Я: Ты же сама радовалась, что я {isBoy(): нашёл| нашла} друзей.
     Я: А теперь заставляешь меня их терять! # З
     Мама: Одна прогулка ничего не решит.
     Я: Как ты не понимаешь? Это важно! # З
     Мама: Я понимаю, но всё уже решено.
     ~ ChangeRelPoints(Parents_RelPoints, -2)
    -> mam_chat
     
*    [(...попытаться убедить маму.) @good]
     Я: Мам, а нельзя что-то изменить? 
     Я: У меня только-только всё налаживается... # П
     Мама: Очень хорошо, что ты со мной делишься.
     Мама: Я это ценю. 
     Мама: Но, понимаешь, иногда обстоятельства против нас. # П
     Мама: Я уверена, что друзья поймут тебя. 
     Я: Я тоже надеюсь. # П
     ~ ChangeRelPoints(Parents_RelPoints, 2)
     -> mam_chat
     
*    [(...просто согласиться.)]
     Я: Ладно... # П
     Я: Останусь дома... # П
     Мама: Я знала, что ты поймёшь. # Р
     -> mam_chat

==mam_chat==
Мама: Так, мне пора.  

# Location room_main

Обычно ты не против посидеть с младшей сестрой. 
Но не сегодня.
Ты вручаешь ей планшет и включаешь мультики. 
{isBoy(): Сам| Сама} же грустно читаешь общий чат. 

Лидер: Все, кто приглашён на сходку! 
Лидер: Поторопитесь! 
Настя: И возьмите свои лучшие лица для селфи! # Р
ЛидерПара: Кто опоздает, сам виноват! 

Ты тяжело вздыхаешь, представляя что будет завтра.
Новые издёвки и подколы...

Приспешник: Я опаздываю :( # П
Лидер: Лучше не приходи тогда! 
ЛидерПара: В топку всех опаздунов! 

Вдруг раздаётся громкий плач сестры.
Ты подходишь к ней. Сестра заливается слезами.

- Я: (Я могу...)
*    [(...спросить, что случилось.)]
     Я: Что такое, сестрёнка? 
     Я: Чего ты плачешь?
     
     Но сестра только сильнее заливается слезами. 
     -> sister
     
*    [(...взять сестру за руку.)]
     Ты берёшь сестру за руку. 
     Она поднимает на тебя заплаканное лицо и указывает на планшет. 
     -> sister

==sister==
Ты смотришь на планшет, лежащий на полу. 

Я: Вот оно что... # П

На планшете крутится какое-то видео.
Один из тех трешевых роликов с кровью и руганью.  

Я: Маленькая, вот чего ты испугалась... # П

Ты выключаешь видео и пытаешься успокоить сестру. 

Я: Прости, пожалуйста. 
Я: Мне нужно было сидеть с тобой.
Я: Я же не могу контролировать всё видео в интернете.

Тут ты вспоминаешь про Лабораторию.
И звонишь туда. 

Мэй: Привет, @playername@!
Мэй: Нужна помощь?
Я: Да, нужна...

- Я: (Я могу...)
*    [(...рассказать про сестру.) @good]
     Я: Понимаешь, я дал младшей сестре планшет... # П
     Мэй: Можешь не продолжать. 
     Мэй: Она увидела то, что маленьким видеть нежелательно.
     Я: Я {isBoy(): виноват| виновата}...
     Мэй: Сейчас это уже не важно. 
     Мэй: Главное, что ты {isBoy(): позвонил| позвонила} мне.
     ~ ChangeRelPoints(Lab_RelPoints, 2)
     -> video_control

*    [(...не упоминать о случившемся.)]
     Я: Хочу дать сестре планшет.
     Я: Но волнуюсь: вдруг она что-то лишнее увидит. 
     Мэй: И правильно делаешь. 
     Мэй: Детей не стоит пускать в интернет без присмотра. 
     Мэй: Я тебе помогу. 
     -> video_control

*    [(...просто спросить о возможности контроля.) @bad]
     Я: Тут, такое дело...
     Я: Можно ли как-то сделать так, чтобы...
     Я: Ну, чтобы на планшете не все сайты открывались?
     Мэй: Можно. А зачем тебе? 
     Я: Да так. Просто любопытно.
     Мэй: Ладно, не хочешь — не говори. # П 
     Мэй: Но и от меня не жди доверия. # П
     ~ ChangeRelPoints(Lab_RelPoints, -2)
     -> video_control

==video_control==
Мэй: На самом деле всё просто. 
Мэй: Тебе нужно установить Kaspersky Safe Kids. 
Я: А что это? 
Мэй: Программа, которая запрещает доступ к взрослым сайтам. 
Мэй: Я пришлю тебе пробную версию. 
Я: Спасибо! # Р

Ты устанавливаешь на планшет программу и играешь с сестрой.
Через полчаса приходит мама.

Я: Ты же говорила, что будешь поздно. # У
Мама: Всё закончилось гораздо быстрее. 
Мама: Можешь бежать к своим друзьям. 
Я: Ура!!! # Р

# Location bus_parking

Быстро одевшись, ты выскакиваешь из дома. 
В ожидании автобуса, читаешь общий чат. 

Лидер: Мы уже на месте!
ЛидерПара: Долго ждать не будем! 
Приспешник: А для инвалидов исключение будет?
Приспешник: Кеды жмут так, что еле иду :((( # П
Лидер: Так уж и быть. Ещё 10 минут.

Похоже, ты успеваешь. 

# Location meteor

Лидер: А вот и {isBoy(): новенький| новенькая}!
Лидер: Ты тоже инвалид? 

- Я: (Я могу...)
*    [(...просто согласиться с этим.) @bad]
     Я: Наверное... # П
     Лидер: И ты так просто это признаёшь?
     ЛидерПара: Так только слабаки поступают. 
     ~ ChangeRelPoints(Leader_RelPoints, -2)
     ->joke

*    [(...согласиться и пошутить.) @good]
     Я: Ага, инвалид по пространственному ориентированию. 
     Приспешник: Чего? # У
     Я: {isBoy(): Думал| Думала} состарюсь, пока пойму на какой автобус сесть.
     Лидер: Ха! Это ты точно {isBoy(): подметил| подметила}! # Р
     Я: Им маршруты, походу, Сусанин прокладывал. 
     ЛидерПара: Точняк! # Р
     ~ ChangeRelPoints(Leader_RelPoints, 2)
     ->joke

*    [(...оправдаться.)]
     Я: Автобусы у вас тут ходят...
     Я: Как вы вообще куда-то успеваете? 
     Лидер: Это точно. 
     ЛидерПара: Автобусы-черепахи.
     ->joke

==joke==
Лидер: Так! Все в сборе! Начинаем! 

Денис взбирается на ограждение и делает селфи.
Другие пытаются следовать его примеру.

Приспешник: На этой селфи-тусовке я заработаю кучу лайков! 
ЛидерПара: Мечтай! Сейчас {isBoy(): новенький| новенькая} тебя сделает!

Ты достаёшь телефон, но Настя тебя останавливает. 

Настя: И чем ты собираешься делать селфи? 
Я: Камерой. Чем же ещё? 
Лидер: Лошара. 
Я: А что не так? 
Настя: Все нормальные люди ставят «ФотоСелф». 
Настя: Без этой приложухи даже не подходи. 
Я: Сейчас установлю. 
Лидер: Ага, давай. Только не светись тут. 
Лидер: В кадр попадёшь. 

Ты отходишь в сторону и замечаешь ангар. 
Тот самый, где вчера прятался от преследователей. 

# Location hangar

Я: Так... И где же это «ФотоСелф»?

Не открывая магазин приложений, ты сразу ищешь в интернете. 

Я: Есть! 

Для того, чтобы скачать программу, нужно отправить СМС.

- Я: (Я могу...)
*    [(...отправить СМС.) @bad]
     Ты отправляешь СМС и в ответ получаешь ссылку. 
     Но программа почему-то не скачивается.
     
     Я: Да что ж такое... # З
     
     Телефон звонит так неожиданно, что ты вздрагиваешь. 
     
     Я: Ааа! # Effect actor_shake
     
     Дин: Что, {isBoy(): допрыгался| допрыгалась}?
     Я: Дин? Что-то случилось? 
     Дин: Конечно! Кто-то просадил все деньги на телефоне.
     Я: Не понимаю... # У
     Дин: А ты баланс проверь. 
     
     Ты послушно проверяешь баланс.
     У тебя на счету -500 рублей.
     
     Я: Ой... # У
     Дин: Вот и ой.
     Дин: В следующий раз не будешь отправлять СМС куда ни попадя.
     Я: Так это из-за той смски? # П
     Дин: Да ты умнеешь на глазах! # Р
     Дин: Жаль только, что после того, как {isBoy(): попал| попала} на деньги. 
     ~ ChangeRelPoints(Lab_RelPoints, -2)
     -> download_sms

*    [(...посоветоваться с Лабораторией.) @good]
     Ты набираешь номер Лаборатории. 
     Дин: Здорово, {isBoy(): чувак| чувиха}!
     Я: А Мэй рядом нет? # У
     Дин: Не-а. Но я тоже ничего выгляжу.
     Дин: Или тебе с гиками родители не разрешают общаться? # У
     Я: Дин, не обижайся. Я {isBoy(): хотел| хотела} посоветоваться.
     Дин: Валяй! # Р
     Я: Мне тут нужно одно приложение на телефон поставить. 
     Я: А чтобы скачать нужно...
     Дин: Отправить всего одно СМС на короткий номер!
     Я: Как ты... # У
     Дин: Бинго! # Р
     Я: Так отправлять?
     Дин: Если у тебя полно денег, отправляй. 
     Дин: А если ты {isBoy(): умный| умная}, то не стоит.
     Дин: Но раз ты сперва {isBoy(): позвонил| позвонила} мне, то ты {isBoy(): умный| умная}.
     ~ ChangeRelPoints(Lab_RelPoints, 2)
     -> download_sms

==download_sms==
Я: Объяснишь, как это работает?
Дин: Легко!
Дин: На многих пиратских сайтах такая система. 
Дин: Прежде чем дать ссылку, тебя просят отправить СМС. 
Дин: Но не говорят, сколько она стоит. 
Я: Много... Я уже {isBoy(): понял| поняла}.
Дин: Вот и отлично.
Дин: Забей на это приложение. Лучше погуляй. 
Дин: Пока!
Я: Пока.

# Location meteor

Ты выходишь из ангара к остальным. 

ЛидерПара: {isBoy(): Поставил| Поставила} «ФотоСелф»? 
Я: Нет. 
Лидер: А чего так? 
- Я: Да вот...
*    Я: ...деньги на телефоне кончились. 
     Лидер: Бывает. 
     Денис: А у меня никогда не заканчиваются. 
     Денис: Автоплатёж рулит. 
     Настя: Точно! 
     -> leader_answer

*    Я: ...на мой телефон она не устанавливается. @bad
     Лидер: Значит, у тебя отстойный телефон. 
     ЛидерПара: А раз у тебя отстойный телефон...
     Приспешник: Ты {isBoy(): сам| сама} отстой! # З
     Тебе ничего не остаётся, как промолчать.
     ~ ChangeRelPoints(Leader_RelPoints, -2)
     -> leader_answer

*    Я: ...не понравилась она мне. @good
     Лидер: Ты чего это? # У
     Приспешник: Это лучшее приложение для селфи! 
     Я: Все фото-приложения всё равно используют штатную камеру.
     Я: Так что они отличаются только встроенными фильтрами. 
     Лидер: Серьёзно? Не {isBoy(): знал| знала}.
     ~ ChangeRelPoints(Leader_RelPoints, 2)
     -> leader_answer

==leader_answer==
Я: Ладно, вы тут развлекайтесь. 
Я: А мне пора. До завтра. 

Ты уходишь, слыша за спиной сдавленные смешки.
Настроение испорчено. 
Чтобы хоть как-то развеселиться, ты заходишь в кафе. 

# Location cafe

Ожидая, пока тебе принесут мороженное, ты пишешь {isBoy(): Вике| Вите}.

Я: Привет.
Симпатия: Привет. 
Симпатия: {isBoy(): Ходил| Ходила} на эту дурацкую селфи-сходку?
Я: Почему дурацкую? 
Симпатия: Потому что селфи — идиотизм.
Я: Это же просто фотография себя. 
Симпатия: А зачем её делают? 

- Я: Ну, чтобы... 
*    Я: ...все видели, {isBoy(): какой я классный| какая я классная}. @bad
     Симпатия: Да ты, я гляжу, {isBoy(): самовлюблённый пижон| самовлюблённая девица}.
     Я: Ну...
     Симпатия: Все эти статусы и лайки совсем не важны...
     ~ ChangeRelPoints(Love_RelPoints, -2)
     -> selfy

*    Я: ...иметь память о событии. 
     Симпатия: А просто фотографию сделать нельзя? 
     Я: Тогда как доказать, что там {isBoy(): был| была} именно я?
     Симпатия: А зачем кому-то что-то доказывать?
     Симпатия: Воспоминание — оно только твоё. 
     Симпатия: Всё остальное не важно...
     -> selfy

*    Я: ...сообщить друзьям, что с тобой всё в порядке. @good
     Симпатия: Хм... Возможно в этом есть смысл. 
     Симпатия: Хотя другу можно просто позвонить. 
     Я: Я {isBoy(): сам| сама} понимаю, что селфи — это глупость. 
     Я: Но это часть современного мира. 
     Симпатия: Увы, да... 
     Симпатия: Хотя все эти «актуальные тренды» не важны.
     ~ ChangeRelPoints(Love_RelPoints, 2)
     -> selfy

==selfy==
Я: А что важно? 
Симпатия: Творчество. Только творчество важно. 
Я: И какое же? 
Симпатия: Хочешь фотографировать?
Симпатия: Делай не дурацкие селфи, а настоящие фотографии. 
Симпатия: Хочешь писать?
Симпатия: Пиши не дурацкие посты в соцсетях, а роман. 
Я: А что делаешь ты? 
Симпатия: Я пишу книгу. 
Я: Ого! И какую? # У
Симпатия: Фанфик во вселенной Трансформеров. 
Я: Круто! Дашь почитать? 
Симпатия: Возможно. Как-нибудь потом. 
Симпатия: А теперь — пока. 
Симпатия: Нужно ещё кучу дел по дому успеть. 
Я: Пока.

Разговор с {isBoy(): Викой| Витей} поднимает тебе настроение.
Ты спокойно возвращаешься домой. 

# Location room_main

Перед сном ты получаешь сообщение. 

Стас: Похоже, я сделал большую глупость... # П
Я: Что случилось?

Но Стас не отвечает. 
Ты засыпаешь, гадая, что же могло произойти.
->END