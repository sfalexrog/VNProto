INCLUDE GameData.ink

-> start

=== start ===

#Location classroom 

Учитель: Здравствуй! Ты, наверное, {isBoy(): новенький | новенькая}?
Учитель: Меня зовут Сергей Олегович.
Учитель: К сожалению, для тебя есть место только на задней парте. 
Учитель: Ты не против? 
Я: Нет, что вы… 
	*[У меня хорошее зрение.]
	Учитель: Вот и отлично. Начинаем урок! # радость
	->lesson_start 

	*[Это моё привычное место.] 
	Учитель: Значит, быстрее освоишься. Начинаем!
	->lesson_start 

	*[У меня всё равно нет выбора.]
	Учитель: Вижу, с тобой будет непросто. # грусть
	->lesson_start

==lesson_start==

Звенит звонок, ученики рассаживаются по местам. 
Учитель: Итак, на этом уроке истории мы поговорим о древних греках. 
Учитель: Они были искусными мореплавателями. 
Учитель: Для своего времени они создавали лучшие морские корабли. 
Учитель: Но об этом расскажу не я. Мне с докладом согласился помочь один из вас.
{isBoy(): Вика| Витя}: Да, древние греки совершили много открытий в мореходстве, например…
Ты удивляешься своей реакции. 
{isBoy(): Она такая милая| Он такой милый}, что ты ничего не понимаешь, только слушаешь приятный голос.
Так незаметно урок подходит к концу. 
Учитель: Скажем спасибо нашему докладчику. 
Учитель: Надеюсь, вам понравилось так же, как и мне.
Учитель: А вот и звонок. До встречи на следующем уроке. 
На перемене ты видишь, как один ученик грубо отталкивает другого. 
{isBoy(): Денис| Настя}: Свин, свали с дороги. # злость
Неуклюжий парень что-то обиженно бормочет, а потом подходит к тебе. 
Стас: Я — Стас. 
Стас: Ну, как тебе в нашем классе? 
Я: Неплохо. 
Стас: Да, у нас неплохой класс, вообще-то... 
Я: Ты оправдываешься? 
Стас: Ну, как сказать. Ты же {isBoy():  видел | видела}... как меня… # грусть 
Я: Назвали Свином?
Стас: Типа да. Мне не нравится, но я терплю.
->dialog1

===dialog1===

Я: Слушай… 
	*[Почему тебя называют Свином?]
	Стас: Ну, была одна история с видео. # грусть
	Я: Не хочешь, не рассказывай.
	Стас: Ты всё равно узнаешь. Лучше уж я расскажу сам.
	Стас: Был случай, я упал в грязь, и это сняли на видео. 
	Я: И что? 
	Стас: Выложили на YouTube и всему классу разослали ссылку. # злость
	Я: Я {isBoy():  видел | видела} подобные ролики. Это забавно.
	Стас: Ага, когда смотришь на других. А когда это ты сам весь в грязи и не можешь встать… # грусть
	Стас: Надо мной половина школы смеялась… С тех пор и кличка эта дурацкая… # грусть
	Я: Прости. Это, действительно неприятно… # грусть
	->dialog1
	
	*[Кто тебя толкнул?]
	Стас: {isBoy(): Денис. Его все боятся в классе. | Настя. Она в классе самая крутая.}
	Я: Мне так не показалось. 
	Стас: Ещё не вечер. Советую тебе не расслабляться. 
	Я: Да я всегда на чеку.
	->dialog1
	*[Давай о чём-нибудь другом?]
	->dialog2

===dialog2===

Стас: Да, конечно! Про кого тебе ещё рассказать?
->dialog3

===dialog3===

Я: … 
	*[А кто делал доклад?]
	Стас: {isBoy(): Это Вика. Она клёвая девчонка | Это Витя. Он неплохой парень}, но немного не в себе.
	Я: Не понимаю.
	Стас: Ну, молчит всё время, ни с кем не общается. 
	Стас: В общих тусовках не участвует. 
	Я: И {isBoy(): Её | Его} не трогают, ну, как…
	Стас:  Как меня? Нет. {isBoy(): Она | Он} просто игнорит всех. 
	Я: Кто-нибудь с {isBoy(): ней | ним} общается?
	Стас: Нет, потому что {isBoy(): она сама | он сам} ни с кем не общается.
	Стас: Ещё что-то хочешь спросить? 
	->dialog3 
	
	*[Расскажи про учителя.]
	Стас: Он клёвый. Мы его зовём Пандой. 
	Я: Почему? # удивление
	Стас: Ну, во-первых, он добрый, как Панда. 
	Я: А во-вторых? 
	Стас: Из-за его очков. # радость
	Стас: Ну, такие огромные чёрные круги вокруг глаз. # радость
	Стас: Вылитый Панда! # радость
	Я: Точно, похож. # радость
	Стас: Он хороший Учитель, только вот слишком добрый. 
	Я: Как это? 
	Стас: Другие ему хамят, а он ничего не может с этим сделать. 
	Стас: Ещё что-нибудь рассказать? 
	->dialog3
	
	*[Пожалуй, пока хватит.]
	->lesson2

==lesson2==

Стас: Как раз звонок.
Панда: Итак, второй урок. Сегодняшняя тема…
Остальные уроки ты слушаешь вполуха. 
Больше смотришь на {isBoy():  загадочную девушку Вику | загадочного парня Витю}.
 
#Location school_yard  

Наконец-то первый день в новой школе закончен. 
Ты собираешься идти домой. 
Стас: Как первый день? Ещё не хочешь сбежать отсюда? # радость
Я: Немного. 
Стас: И я хочу. Можем сбежать вместе. 
Стас: Как тебе идея? 
{isBoy(): Денис| Настя}: Эй, Свин, я знаю, за школой отличную лужу. Тебе показать? 
Стас: Отстань. # злость # Effect character_shake
{isBoy(): Денис| Настя}: Ты чего там хрюкнул? # злость

Я: (Я могу…) 
	*[(Заступиться за Стаса.)]
	Я: Слушай, я тут со Стасом разговариваю. 
	Я: Ты нам мешаешь. 
	{isBoy(): Денис| Настя}: Ой, посмотрите! У нашей свинки появился заступник. # удивление # Effect character_shake
	Стас: Не стоит, идём отсюда. 
	{isBoy(): Денис| Настя}: Вот и чеши давай! У меня тут разговор. # злость
	Стас, низко опустив голову, быстро уходит прочь. 
	{isBoy(): Денис| Настя}: Я тебя прощаю на первый раз. 
	{isBoy(): Денис| Настя}: Но больше не стой у меня на пути, ясно тебе?
	Я: А если встану? 
	{isBoy(): Денис| Настя}: Тогда ты пожалеешь, что {isBoy(): попал | попала} в эту школу! # злость
	{isBoy(): Денис| Настя}: Вали отсюда, пока у меня настроение хорошее! # злость
	Ты спешно уходишь, но краем глаза успеваешь заметить, как {isBoy(): Вика смотрит тебе вслед | Витя смотрит тебе вслед}.  
	{isBoy(): Вика оценила , что ты вступился за Стаса. | Витя оценил, что ты вступилась за Стаса.} 
	->DONE

	*[(Не вмешиваться.)]
	Стас: Я не с тобой говорю.
	{isBoy(): Денис| Настя}: А тебе никто и не позволял открывать рот. # злость
	Стас: Чего ты хочешь? # злость
	{isBoy(): Денис| Настя}: Чтобы ты свалил отсюда! # злость
	Стас, низко опустив голову, быстро уходит прочь. 
	{isBoy(): Денис| Настя}: Ну, привет, что ли. 
	Я: Привет. 
	{isBoy(): Денис| Настя}: Вижу, ты не понимаешь, как у нас всё устроено. 
	Я: Так объясни. 
	{isBoy(): Денис| Настя}: Что за наглость? # удивление
	{isBoy(): Денис| Настя}: Новенькие должны опускать глаза и молчать, когда я говорю! # удивление
	Я: Может тебе ещё сопли вытереть?
	{isBoy(): Денис| Настя}: Послушай-ка, ты… # злость
	{isBoy(): Денис| Настя}: Хочешь жить, не высовывайся! # злость
	Ты спешно уходишь, но видишь, как одноклассники смотрят на тебя. 
	Одноклассники оценили, что ты {isBoy(): вёл | вела} себя смело с лидером класса.
	
	->END
